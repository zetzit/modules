using io;
using err;
using time;

using <unistd.h> as unistd;
using <string.h>::{memset};
using <errno.h>::{errno, EAGAIN};
using <sys/select.h>::{
    select,
    fd_set,
    FD_ISSET,
    FD_SET,
    FD_ZERO
};
using <stdio.h>::{printf, STDIN_FILENO};
using <limits.h>::{INT_MAX};
using <sys/time.h>::{(struct timeval) as timeval_t};
using <unistd.h>::{usleep};
using log;


export struct Async {
    io::Async   base;
    int         timeout;
    usize       count;

    fd_set      read_fds;
    fd_set      write_fds;
    int         maxfd;
}

export fn make(Async mut new *self) {
    memset(self, 0, (usize)sizeof(Async));

    self->base.impl_make_timeout    = impl_make_timeout;
    self->base.impl_make_channel    = impl_make_channel;
    self->base.impl_select          = impl_unix_select;
    self->base.impl_wake            = impl_wake;
    self->base.impl_wait            = impl_wait;
    self->base.upper                = self;

    self->reset();
}

fn reset(Async mut *self)
{
    self->count     = 0;
    self->timeout   = -1;

    FD_ZERO(&self->read_fds);
    FD_ZERO(&self->write_fds);
    self->maxfd = 0;
}

pub fn select_read_fd(Async mut* self, int fd) -> bool {
    FD_SET(fd, &self->read_fds);
    if fd > self->maxfd {
        self->maxfd = fd;
    }
    return true;
}

pub fn select_write_fd(Async mut* self, int fd) -> bool {
    FD_SET(fd, &self->write_fds);
    if fd > self->maxfd {
        self->maxfd = fd;
    }
    return true;
}

fn impl_unix_select(io::Async mut* async, err::Err+et mut*e, io::Context mut*ctx, io::Ready w)
    where err::checked(*e)
{
    let self = (Async mut *)async->upper;
    static_attest(safe(self));

    if ctx->async == 0 {
        err::fail(e, err::InvalidArgument, "not an async io");
        return;
    }

    if ctx->fd != 0 {
        if !select_read_fd(self, ctx->fd) {
            err::fail(e,err::OutOfTail,"too many fds");
            return;
        }
    }

    if ctx->time != 0 {
        time::Time now  = time::tick();
        u64 mut now_m = now.to_millis();
        u64 remains = ctx->time - now_m;
        if self->timeout < 0 || (u64)self->timeout > remains {
            self->timeout = (int)remains;
        }
    }
}


fn impl_wait(io::Async mut* async, err::Err+et mut*e)
    where err::checked(*e)
{
    let self = (Async mut *)async->upper;
    static_attest(safe(self));

    printf(">> io::select::wait %d (maxfd: %d)\n", self->timeout, self->maxfd);


    timeval_t mut timeout = {0};
    unsafe{
        timeout.tv_sec  = self->timeout / 1000;
        timeout.tv_usec = (self->timeout % 1000) * 1000;
    }

    timeval_t mut * mut timeout_p = &timeout;
    if self->timeout < 0 {
        timeout_p = 0;
    }

    int rr = unsafe<int>(select(
        self->maxfd + 1,
        &self->read_fds,
        &self->write_fds,
        0,
        timeout_p
    ));

    log::debug("io::select::wait %d\n", rr);

    self->reset();
    if rr < 0 {
        err::fail_with_errno(e, "poll");
        return;
    } else if rr == 0 {
        usleep(1000);
    }
}

export fn unix(int fd) -> io::Io
    model(safe(return.read_impl))
{
    io::Context ctx = io::Context{
        fd:         fd,
        isvalid:    true,
    };

    return io::Io {
        read_impl:  impl_unix_read,
        write_impl: impl_unix_write,
        close_impl: impl_unix_close,
        ctx:        ctx,
    };
}

export fn stdin() -> io::Io
    model(safe(return.read_impl))
{
    return unix(as<int>(STDIN_FILENO));
}

using <fcntl.h>::{fcntl, F_GETFL, O_NONBLOCK, F_SETFL};

export fn make_read_async(io::Io mut *i, io::Async mut* unsafe async) -> bool {

    (i->ctx).async = async;

    int mut flags = (int)fcntl((i->ctx).fd, F_GETFL, 0);
    if (flags == -1) {
        return false;
    }
    flags = flags | (int)O_NONBLOCK;

    return ((int)fcntl((i->ctx).fd, F_SETFL, flags) == 0);
}

fn impl_unix_read(io::Context mut*ctx, err::Err+et mut*e, u8 mut* to, usize mut *l) -> io::Result
    where err::checked(*e)
{
    if ctx->async != 0 {
        static_attest(safe(ctx->async));
        if !select_read_fd((Async mut*)ctx->async, ctx->fd) {
            err::fail(e, err::OutOfTail, "too many fds");
            return io::Result::Error;
        }
    }

    if *l == 0 {
        err::fail(e, err::InvalidArgument, "buffer is 0 long");
        return io::Result::Error;
    }

    isize rr = (isize)unistd::read(ctx->fd, to, *l);
    if rr < 0 {
        if (int)errno == (int)EAGAIN {
            return io::Result::Later;
        }
        err::fail_with_errno(e, "read");
        return io::Result::Error;
    } else if rr == 0 {
        return io::Result::Eof;
    }
    *l= (usize)rr;
    return io::Result::Ready;
}


fn impl_unix_write(io::Context mut*ctx, err::Err+et mut*e, u8 * to, usize mut *l) -> io::Result
    where err::checked(*e)
{
    for (usize mut attempt = 0;attempt<100;attempt++) {
        isize rr = (isize)unistd::write(ctx->fd, to, *l);
        if rr < 0 {
            if (int)errno == (int)EAGAIN {
                usleep(1000);
                continue;
            }
            err::fail_with_errno(e, "read");
            return io::Result::Error;
        }
        *l= (usize)rr;
        return io::Result::Ready;
    }
    static_attest(err::checked(*e));
    err::fail_with_errno(e, "read");
    return io::Result::Error;
}

fn impl_unix_close(io::Context mut* ctx)
{
    if !ctx->isvalid  {
        return;
    }
    unistd::close(ctx->fd);
    ctx->isvalid = false;
}


fn impl_wake(io::Async mut *async)
{
    let self= (Async mut *)async->upper;
    static_attest(safe(self));

    self->timeout = 0;
}

fn impl_make_timeout(io::Async mut *async, err::Err+et mut*e, time::Time t2) -> io::Io
    where err::checked(*e)
{
    if !t2.finite {
        return io::Io {
            read_impl:  impl_never,
        };
    }

    time::Time now = time::tick();
    u64 mut expires = now.to_millis();
    expires += t2.to_millis();

    io::Context ctx = io::Context{
        time:       expires,
        async:      (io::Async mut*)async,
        isvalid:    true,
    };

    return io::Io {
        read_impl:  impl_timeout_read,
        close_impl: impl_timer_close,
        ctx:        ctx,
    };
}

fn impl_never(io::Context mut*ctx, err::Err+et mut*e, u8 mut* to, usize mut *l) -> io::Result
{
    return io::Result::Later;
}

fn impl_timeout_read(io::Context mut*ctx, err::Err+et mut*e, u8 mut* to, usize mut *l) -> io::Result
    where err::checked(*e)
{

    *l = 0;

    if ctx->time == 0 {
        return io::Result::Eof;
    }

    time::Time now  = time::tick();
    u64 mut now_m = now.to_millis();
    if now_m >= ctx->time {
        return io::Result::Ready;
    } else {
        Async mut* self = (Async mut*)ctx->async;
        static_attest(safe(self));
        u64 remains = ctx->time - now_m;
        if self->timeout < 0 || (u64)self->timeout > remains {
            self->timeout = (int)remains;
        }
    }
    return io::Result::Later;
}

fn impl_timer_close(io::Context mut*ctx) {
    ctx->time     = 0;
    ctx->isvalid  = false;
}

fn impl_make_channel(io::Async mut *async, err::Err+et mut*e, io::Io mut*read, io::Io mut*write)
    where err::checked(*e)
{
    e->fail(err::NotImplemented, "pipe noimpl");
}
